# RFC3875 - The Common Gateway Interface

## Basic Rules

```
alpha         = lowalpha | hialpha
lowalpha      = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" |
                "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" |
                "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" |
                "y" | "z"
hialpha       = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" |
                "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" |
                "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" |
                "Y" | "Z"
digit         = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
alphanum      = alpha | digit
OCTET         = <any 8-bit byte>
CHAR          = alpha | digit | separator | "!" | "#" | "$" |
                "%" | "&" | "’" | "*" | "+" | "-" | "." | "‘" |
                "^" | "_" | "{" | "|" | "}" | "˜" | CTL
CTL           = <any control character>
SP            = <space character>
HT            = <horizontal tab character>
NL            = <newline>
LWSP          = SP | HT | NL
separator     = "(" | ")" | "<" | ">" | "@" | "," | ";" | ":" |
                "\" | <"> | "/" | "[" | "]" | "?" | "=" | "{" |
                "}" | SP | HT
token         = 1*<any CHAR except CTLs or separators>
quoted-string = <"> *qdtext <">
qdtext        = <any CHAR except <"> and CTLs
reserved      = ";" | "/" | "?" | ":" | "@" | "&" | "=" | "+" | "$" |
                "," | "[" | "]"
hex           = digit | "A" | "B" | "C" | "D" | "E" | "F" | "a" | "b"
                | "c" | "d" | "e" | "f"
escaped       = "%" hex hex
unreserved    = alpha | digit | mark
mark          = "-" | "_" | "." | "!" | "˜" | "*" | "’" | "(" | ")"
```

## Script Selection
The server determines which CGI is script to be executed based on a generic-form URI supplied by the client. The remainder of the path, if any, is a resource or sub-resource identifier to be interpreted by the script as meta-variable (`= <extra-path>`).

## The Script-URI
The Script-URI has the structure of a generic URI:
```
script-URI = <scheme> "://" <server-name> ":" <server-port>
             <script-path> <extra-path> "?" <query-string>
```
where `<scheme>` is found from SERVER_PROTOCOL, `<server-name>`,
`<server-port>` and `<query-string>` are the values of the respective
meta-variables.

# The CGI Request
Information about a request comes from two different sources; the request meta-variables and any associated message-body.

## Request Meta-Variables

```
meta-variable-name  = "AUTH_TYPE"       | "CONTENT_LENGTH"    |
                      "CONTENT_TYPE"    | "GATEWAY_INTERFACE" |
                      "PATH_INFO"       | "PATH_TRANSLATED"   |
                      "QUERY_STRING"    | "REMOTE_ADDR"       |
                      "REMOTE_HOST"     | "REMOTE_IDENT"      |
                      "REMOTE_USER"     | "REQUEST_METHOD"    |
                      "SCRIPT_NAME"     | "SERVER_NAME"       |
                      "SERVER_PORT"     | "SERVER_PROTOCOL"   |
                      "SERVER_SOFTWARE" | scheme              |
                      protocol-var-name | extension-var-name
protocol-var-name   = ( protocol | scheme ) "_" var-name
scheme              = alpha *( alpha | digit | "+" | "-" | "." )
var-name            = token
extension-var-name  = token
meta-variable-value = "" | 1*<TEXT, CHAR or tokens of value>
```
- **Meta-variables are identified by case-insensitive names.**
- **Meta-variable values MUST be considered case-sensitive except as noted otherwise.**
- This specification does not distinguish between zero-length (NULL) values and missing values.  
For example, a script cannot distinguish between the two requests `http://host/script` and `http://host/script?` as in both cases the `QUERY_STRING` meta-variable would be NULL.

### AUTH_TYPE
The AUTH_TYPE variable identifies any mechanism used by the server to authenticate the user.
> **NOT REQUIRED BY THE SUBJECT**

### CONTENT_LENGTH
The size of the message-body attached to the request, if any, in decimal number of octets. If no data is attached, then NULL (or unset).  
The server MUST set this meta-variable if and only if the request is accompanied by a message-body.

### CONTENT_TYPE
If the request includes a message-body, the CONTENT_TYPE variable is set to the Internet Media Type of the message-body

```
CONTENT_TYPE = "" | media-type
media-type   = type "/" subtype *( ";" parameter )
type         = token
subtype      = token
parameter    = attribute "=" value
attribute    = token
value        = token | quoted-string
```

- The type, subtype and parameter attribute names are not case-sensitive.
- Parameter values may be case sensitive.
- There is no default value for this variable. If and only if it is unset, then the script MAY attempt to determine the media type from the data received.
- **The server MUST set this meta-variable if an HTTP Content-Type field is present in the client request header, otherwise it should omit this meta-variable.**

### GATEWAY_INTERFACE
RFC3875 defines the 1.1 version of the CGI interface.
> `GATEWAY_INTERFACE = CGI/1.1`

### PATH_INFO
- The PATH_INFO variable is derived from the portion of the URI path hierarchy following the part that identifies the script itself.
- the PATH_INFO is not URL-encoded.
- **case-sensitive and the server MUST preserve the case of the path as presented in the request URI.**
- The server MAY impose restrictions and limitations and MAY reject the request with an error.
- PATH_INFO string forms the extra-path component of the Script-URI

### PATH_TRANSLATED
The PATH_TRANSLATED variable is derived by taking the PATH_INFO value and performing any virtual-to-physical translation appropriate to map it onto the server’s document repository structure.

- The server MUST preserve the case of the extra-path segment
- The server SHOULD set this meta-variable if the request URI includes a path-info component.
- If PATH_INFO is NULL, then the `PATH_TRANSLATED` variable MUST be set to NULL (or unset).

#### Example
A request such as the following:
```
http://somehost.com/cgi-bin/somescript/this%2eis%2epath%3binfo
```
Would result in a `PATH_INFO` value of `/this.is.the.path;info`. 
An internal URI is constructed from the scheme, server location and the URL-encoded PATH_INFO:
`http://somehost.com/this.is.the.path%3binfo`
This would then be translated to a location in the server’s document repository (= `PATH_TRANSLATED`), perhaps a filesystem path something like this:
`/usr/local/www/htdocs/this.is.the.path;info`

### QUERY_STRING
The QUERY_STRING value provides the query-string part of the Script-URI.
 - **The server MUST set this variable;** if the Script-URI does not include a query component, the QUERY_STRING MUST be defined as an empty string ("").

### REMOTE_ADDR
```
REMOTE_ADDR = ipv4-address
```
- The REMOTE_ADDR variable MUST be set to the network address of the client sending the request to the server.

### REMOTE_HOST
The REMOTE_HOST variable contains the fully qualified domain name of the client sending the request to the server, if available, otherwise NULL.

```
REMOTE_HOST   = "" | hostname | hostnumber
hostname      = *( domainlabel "." ) toplabel [ "." ]
domainlabel   = alphanum [ *alphahypdigit alphanum ]
toplabel      = alpha [ *alphahypdigit alphanum ]
alphahypdigit = alphanum | "-"
```

- Domain names are not case sensitive.

### REMOTE_IDENT
The server may choose not to support this feature.
> **NOT REQUIRED BY THE SUBJECT**

### REMOTE_USER
The REMOTE_USER variable provides a user identification string supplied by client as part of user authentication (HTTP Authentication).
> **NOT REQUIRED BY THE SUBJECT**

### REQUEST_METHOD
The REQUEST_METHOD meta-variable MUST be set to the method which should be used by the script to process the request.

```
REQUEST_METHOD = "GET" | "POST" | "HEAD" | "PUT" | "DELETE"
```

### SCRIPT_NAME
The SCRIPT_NAME variable MUST be set to a URI path (not URL-encoded) which could identify the CGI script.
```
SCRIPT_NAME = "" | ( "/" path )
```
- The leading "/" is not part of the path.
- It is optional if the path is NULL; however, the variable MUST still be set in that case.
- No `PATH_INFO` segment is included in the `SCRIPT_NAME` value.

### SERVER_NAME
The SERVER_NAME variable MUST be set to the name of the server host to which the client request is directed.
```
SERVER_NAME = hostname | ipv4-address
```

- It is a case-insensitive hostname or network address.
- It is the contents of the request’s Host header field

### SERVER_PORT
The SERVER_PORT variable MUST be set to the TCP/IP port number on which this request is received from the client.

- **Note that this variable MUST be set.**

### SERVER_PROTOCOL

```
SERVER_PROTOCOL = HTTP/1.1
```

- It is not case sensitive and is usually presented in upper case.

### SERVER_SOFTWARE
The SERVER_SOFTWARE meta-variable MUST be set to the name and version of the information server software making the CGI request.

### Protocol-Specific Meta-Variables
- The HTTP header field name is converted to upper case, has all occurrences of "-" replaced with "_" and has "HTTP_" prepended to give the meta-variable name.
- **The server is not required to create meta-variables for all the header fields that it receives.**

### Request Message-Body
Request data is accessed by the script in a system-defined method. This will be by reading the ’standard input’ file descriptor or file handle.
- A request-body is supplied with the request if the `CONTENT_LENGTH` is not NULL.
- The server MAY signal an end-of-file condition after CONTENT_LENGTH bytes have been read or it MAY supply extension data.
- As *transfer-codings are not supported on the request-body*, the server MUST remove any such codings from the message-body, and recalculate the `CONTENT_LENGTH`.

## Request Methods

### GET
The GET method indicates that the script should produce a document based on the meta-variable values.

### POST
The POST method is used to request the script to perform processing and produce a document based on the data in the request message-body, in addition to meta-variable values.

### HEAD
The HEAD method requests the script to do sufficient processing to return the response header fields, without providing a response message-body.

# CGI Response

## Response Handling
- A script MUST always provide a non-empty response.
- Unless defined otherwise, this will be via the ’standard output’ file descriptor.
- The server MAY implement a timeout period within which data must be received from the script.

## Response Types
The response comprises a message-header and a message-body, separated by a blank line. The body may be NULL.
```
generic-response = 1*header-field NL [ response-body ]
```
The script MUST return one of either
 - a document response,
 - a local redirect response,
 - a client redirect (with optional document) response.

### Document Response

```
document-response = Content-Type [ Status ] *other-field NL response-body
```
- The script MUST return a Content-Type header field.
- A Status header field is optional, and status 200 ’OK’ is assumed if it is omitted.

### Local Redirect Response
The CGI script can return a URI path and query-string for a local resource in a Location header field.
This indicates to the server that it should reprocess the request using the path specified.

```
local-redir-response = local-Location NL
```
- The script MUST NOT return any other header fields.

The server MUST generate the response that it would have produced in response to a request containing the URL
```
scheme "://" server-name ":" server-port local-pathquery
```

### Client Redirect Response
The CGI script can return an absolute URI path in a Location header field, to indicate to the client that it should reprocess the request using the URI specified.

```
client-redir-response = client-Location *extension-field NL
```
- The script MUST NOT return any other header fields.
- **For an HTTP client request, the server MUST generate a 302 ’Found’ HTTP response message.**

### Client Redirect Response with Document
The CGI script can return an absolute URI path in a Location header field together with an attached document, to indicate to the client that it should reprocess the request using the URI specified.

```
client-redirdoc-response = client-Location Status Content-Type
                           *other-field NL response-body
```
- **The Status header field MUST be supplied and MUST contain a status value of 302 ’Found’.**

## Response Header Fields
The response header fields are either CGI or extension header fields to be interpreted by the server, or protocol-specific header fields to be included in the response returned to the client.

```
header-field    = CGI-field      | other-field
CGI-field       = Content-Type   | Location        | Status
other-field     = protocol-field | extension-field
protocol-field  = generic-field
extension-field = generic-field
generic-field   = field-name ":" [ field-value ] NL
field-name      = token
field-value     = *( field-content | LWSP )
field-content   = *( token | separator | quoted-string )
```
- The field-name is not case sensitive.
- A NULL field value is equivalent to a field not being sent.
- Whitespace is permitted between the ":" and the field-value but not between the field-name and the ":".

### Content-Type
The Content-Type response field sets the Internet Media Type:
```
Content-Type = "Content-Type:" media-type NL
```
- If an entity body is returned, the script MUST supply a Content-Type field in the response.
- the default charset assumed by the client for text media-types is `ISO-8859-1`

### Location
The Location header field is used to specify to the server that the script is returning a reference to a document rather than an actual document.
It is either 
- an absolute URI (optionally with a fragment identifier), indicating that the client is to fetch the referenced document, (NDLR: = URI, starts with the name of scheme)
- or a local URI path (optionally with a query string), indicating that the server is to fetch the referenced document and return it to the client as the response. (NDLR: = absolute path `/...`)

### Status
Same as HTTP:
```
Status = "Status:" status-code SP reason-phrase NL
```

### Protocol-Specific Header Fields
May need to transform LF to CRLF.

### Extension Header Fields
The server MAY ignore (and delete) any unrecognised header fields with names beginning "XCGI-" that are received from the script.
> **NOT REQUIRED BY THE SUBJECT**

### Response Message-Body
he server MUST read all the data provided by the script, until the script signals the end of the message-body by way of an end-of-file condition.

# System Specifications

## UNIX / POSIX
### Meta-Variables
Identically named environment variables.
### The current working directory
The current working directory for the script SHOULD be set to the directory containing the script.
### Character set
The US-ASCII character set, excluding NUL, is used for the definition of meta-variables, header fields and CHAR values; TEXT values use ISO-8859-1.
> **US-ASCII REQUIRED BY 42**
